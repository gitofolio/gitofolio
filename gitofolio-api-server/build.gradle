plugins {
	id 'org.springframework.boot' version '2.5.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "org.asciidoctor.jvm.convert" version "3.3.2"
	id 'java'
	id 'war'
}

group = 'com.gitofolio'
version = '0.2.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	runtimeOnly 'mysql:mysql-connector-java'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
	// spring rest docs
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc' 
	
	// jsp
	implementation 'javax.servlet:jstl'
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'
	
	// httpclient
	implementation 'org.apache.httpcomponents:httpclient:4.5.13'
	
	// jwt
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
}

ext { 
	snippetsDir = file('build/generated-snippets')
}

test { 
	outputs.dir snippetsDir
	useJUnitPlatform()
}

asciidoctor { 
	inputs.dir snippetsDir 
	dependsOn test 
}

asciidoctor.doFirst {
    delete file('src/main/webapp/html/docs')
}

bootWar {
	dependsOn asciidoctor 
	from ("${asciidoctor.outputDir}/html5") {
		
		into 'static/docs'
	}
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc/")
    into file("src/main/webapp/html/docs")
}

bootWar {
    dependsOn copyDocument
}
